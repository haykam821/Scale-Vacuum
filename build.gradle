plugins {
	id "fabric-loom" version "0.11.32"
	id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
}

loom {
	runs {
		datagen {
			server()

			name = "Data Generation"
			runDir = "build/datagen"

			vmArg("-Dfabric-api.datagen")
			vmArg("-Dfabric-api.datagen.output-dir=${file("src/main/generated")}")
		}
	}
}
assemble.dependsOn(runDatagen)

sourceSets {
	main {
		resources {
			srcDirs += [
				"src/main/generated"
			]
		}
	}
}

dependencies {
	// Main
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Fabric API
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	// Cardinal Components
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}")
	include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}")

	// Cardinal Components (World)
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cardinal_components_version}")
	include("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cardinal_components_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
}